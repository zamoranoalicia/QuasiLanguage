program ::= PROGRAM ID SEMI block DOT

block ::= declarations (procedure)* compound_statement

declarations ::= VAR (variable_declaration SEMI)+
                 | empty

procedure ::= PROCEDURE ID LPAREN params RPAREN SEMI declarations compound_statement SEMI

params ::= ID COLON type_spec (COMMA params)*

variable_declaration ::= ID (COMMA ID)* COLON type_spec

type_spec ::= INTEGER | REAL

compound_statement ::= BEGIN statement_list END

statement_list ::= statement
               | statement SEMI statement_list

statement ::= compound_statement
          | assignment_statement
          | empty

assignment_statement ::= variable ASSIGN expr

empty ::=

expr::= term ((PLUS | MINUS) term)*

term::= factor ((MUL | DIV) factor)*

factor ::= PLUS factor
       | MINUS factor
       | INTEGER
       | LPAREN expr RPAREN
       | variable

variable::= ID