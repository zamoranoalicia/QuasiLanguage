program ::= PROGRAM ID SEMI block DOT

block ::= declarations compound_statement

declarations ::= variable_declarations
               | procedure_declarations
               | empty

variable_declarations ::= VAR (variable_declaration SEMI)+

procedure_declarations ::= PROCEDURE ID LPAREN parameter_list RPAREN SEMI block SEMI

parameter_list ::= parameter
                | parameter COMMA parameter_list
                | empty

parameter ::= ID COLON type_spec

variable_declaration ::= ID (COMMA ID)* COLON type_spec

type_spec ::= INTEGER | REAL

compound_statement ::= BEGIN statement_list END

statement_list ::= statement
               | statement SEMI statement_list

statement ::= compound_statement
          | assignment_statement
          | procedure_call
          | empty

assignment_statement ::= variable ASSIGN expr

procedure_call ::= ID LPAREN argument_list RPAREN

argument_list ::= expr
               | expr COMMA argument_list
               | empty

empty ::= /* empty */

expr ::= term ((PLUS | MINUS) term)*

term ::= factor ((MUL | DIV) factor)*

factor ::= PLUS factor
       | MINUS factor
       | INTEGER
       | LPAREN expr RPAREN
       | variable

variable ::= ID